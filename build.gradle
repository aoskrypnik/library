plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.github.node-gradle.node' version '3.0.1'
}

group = 'com.ukma'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

node {
    download = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

processResources {
    dependsOn 'copyFrontendToBuild'
}

bootJar {
    archiveFileName = "app.jar"
}

task copyFrontendToBuild(type: Copy) {
    dependsOn 'npmBuild'
    from "$projectDir/src/main/resources/frontend/dist/"
    into "$buildDir/resources/main/static"
}

task npmBuild(type: Exec) {
    workingDir './src/main/resources/frontend'
    commandLine 'npm','run','build'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.amazonaws:aws-java-sdk:1.11.965'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}
